Title: [基础] AHK 中 % 符号的用法
Date: 2016-04-30 15:43
Category: AHK

AHK 中有一个很特别的符号 %。几乎随便从网上下一段 AHK 代码，就可以看到里边有 % 符号，而且 % 在 AHK 里的用法和所有其他编程语言都不一样，基本靠猜的话是猜不出来的。

## 用法一：%var%

第一种用法我们在介绍赋值表达式的时候接触过：

```
a = 123
b = %a%
c = %a%456
```

当用 = 对变量赋值时，如果一个字符串被两个 % 包围，并且中间没有空格，那么含义是取这个变量的值。

在调用一个命令时，也是这样。

```
a = 123
MsgBox, %a%
```

如果我们这样用，`Name, xx, yy`，Name 就是命令；如果我们这样用，`Name("xx", "yy")`，Name 就是函数。命令和函数的区别我们也会在之后的文章了解到，现在只需要关注命令即可。

注意两个%之间只能有一个单独的变量名，像 x[1]、x.y、x[y]、fun() 等一概不支持。

## 用法二：% var

% 还有另一种用法，这回它只出现一个，并且 % 的后边有至少一个空格。

```
a = 123
b = % a
MsgBox, % a
```

这样用表示 % 后边的内容按表达式来解析。我们可以认为：

```
b = %a%
MsgBox, %a%
```

和

```
b = % a
MsgBox, % a
```

是一样的，实际上后者更强大一些。

```
a := 1
b := 2
c := 3

; 计算 a + b + c，结果为 6
MsgBox, % a + b + c

; 语法错误
; MsgBox, %a + b + c%

; 语法正确，但 + 失去计算功能，变成了字符串的一部分
; MsgBox, %a% + %b% + %c%
```

如果 % 后边不只是一个变量名，而是一个表达式，那么“用法二”有效，“用法一”无效。

## 使用“用法一”还是“用法二”

我们发现“用法一”和“用法二”的功能上是有重叠的，那么实际情况应该怎么用呢？这个还是因人而异，尽量前后一致即可。但在某些场景“用法一”更方便，某些情况“用法二”更方便，甚至只能用“用法二”。

“用法一”更方便的场景：

```
; 变量本身包含双引号等特殊字符
c = "%a% %b%"

; 多行字符串中包含变量
c =
(
xxx
%a%
%b%
)
```

“用法二”更方便的场景：

```
; 字符串中的变量居多
MsgBox, % a " " b " " c " " d
```

只能用“用法二”的场景：

```
; 需要使用表达式
MsgBox, % a + b - c
```

另外如果考虑性能的话，“方法一”是要比“方法二”快二分之一到一倍的，但一般情况瓶颈不应该出现在这里，所以也不用过多在乎性能问题，如果真的是因为在很大的循环里使用而产生性能问题，修改一下也是很轻松的事情。
