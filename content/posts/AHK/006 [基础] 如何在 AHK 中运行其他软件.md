Title: [基础] 如何在 AHK 中运行其他软件
Date: 2016-04-28 14:12
Category: AHK

前几篇文章讲的都是 AHK 的语法，比较枯燥，这篇文章的内容更实用一些。很多情况我们使用 AHK，并不是为了写一个功能完备软件，而是写一个简单的脚本来和其他软件交互，这就涉及到如何在 AHK 中运行其他软件。本文重点讲 Run 命令的基本用法，其他高级内容之后的文章会依次提及。

## 启动软件

使用 AHK 的经典场景，就是用一个热键启动一个软件，比如用 Win + n 启动记事本：

```
#n::
    Run, notepad
return
```

这里就不详细讲绑定热键的语法了，因为相关资料特别多，而且我以后会单独介绍热键相关的内容。重点看 Run, notepad，这条语句就是启动记事本，因为记事本软件的文件名是 notepad.exe，是不是非常简单。但 Run 命令也并非简单到只支持一个参数，它的用法如下：

```
Run, Target [, WorkingDir, Max|Min|Hide|UseErrorLevel, OutputVarPID]
```

Target 可以是可执行文件的文件名，如上边的 notepad，可以加参数，也可以是一些其他东西，后文会具体讲到。WorkingDir 用来指定工作目录，比如我想在 C 盘根目录运行记事本并打开 test.txt，就可以这样：

```
Run, notepad test.txt, C:\
```

第三个参数 Max|Min|Hide|UseErrorLevel 是选项，可以让运行的软件最大化（Max），最小化（Min），隐藏（Hide），UseErrorLevel 用于获取进程退出的返回码，暂时用不到，以后再讲。

第四个参数 OutputVarPID 用来获取进程的 PID，暂时也用不到。

需要注意的一点，用 Run 启动其他软件后，AHK 代码会继续执行，而不会等软件退出。如果需要等软件退出再继续运行，需要用 RunWait 命令，这个后续我也会提到。

## 打开网址

Run 后边除了加可执行文件，还可以直接加网址，这样就不要到处找浏览器的安装位置了。

```
; 网址前边需要加 http:// 或 https://，以便让 AHK 知道这是一个网址
Run, https://autohotkey.com
; 如果网址是以 www. 开头的，也可以省略 http:// 或 https://
Run, www.autohotkey.com
; 错误用法，AHK 会尝试在本地找 autohotkey.com 文件，如果找不到就会报错
Run, autohotkey.com
```

## 其他用法

除了启动进程和打开网址，Run 还有一些其他用法。

打开文件。当 Run 后边直接接非可执行文件时，效果也是打开文件。打开文件所使用的软件，是在资源管理器关联的默认软件，可以在资源管理器中选定文件，右键菜单->属性 里更改打开方式。

```
Run, open C:\test.txt
; open 可以省略
Run, C:\test.txt
```

编辑文件。效果同在资源管理器选定文件，右键菜单->编辑，通常是用记事本打开。如果右键菜单里没有“编辑”菜单项，会报错。

```
Run, edit C:\test.txt
```

用资源管理器打开文件夹，需要加文件夹的路径。

```
Run, explore C:\
```

查看文件属性，效果同在资源管理器选定文件，右键菜单->属性。

```
Run, properties C:\test.txt
```

用资源管理器打开文件夹，并进入搜索模式，以便查找文件。

```
Run, find C:\
```

打印文件，效果同在资源管理器选定文件，右键菜单->打印，如果无“打印”菜单项，会报错。

```
Run, print C:\test.txt
```

用资源管理器打开 CLSID 文件夹，CLSID 文件夹是由系统定义的一批特殊文件夹，名字是一串“神秘数字”。

```
; 打开我的文档文件夹
Run, ::{450d8fba-ad25-11d0-98a8-0800361b1103}
```

常用 CLSID 文件夹列表：

CLSID | 名称
----- | -----
::{20d04fe0-3aea-1069-a2d8-08002b30309d} | 我的电脑
::{450d8fba-ad25-11d0-98a8-0800361b1103} | 我的文档
::{645ff040-5081-101b-9f08-00aa002f954e} | 回收站
::{208d2c60-3aea-1069-a2d7-08002b30309d} | 网上邻居
::{7007acc7-3202-11d1-aad2-00805fc1270e} | 网络连接 
::{2227a280-3aea-1069-a2de-08002b30309d} | 打印机和传真 

## 关于空格和引号

如果文件名、路径名或者参数中包含空格，是需要特别注意的。

Run 后边加的文件路径是可以包含空格的，比如：

```
Run, c:\Program Files\Oracle\VirtualBox\VirtualBox.exe
```

Program Files 里包含一个空格，但 Run 不会将 c:\Program 视为文件名，将 Files\Oracle\VirtualBox\VirtualBox.exe 视为参数。但如果之后再加参数就要小心了，谨慎起见，需要对每一个参数单独加上双引号。

虽然在某些情况不加也可以工作，比如这样：

```
Run, notepad aa bb.txt
```

会用记事本打开一个名为 aa bb.txt 的文件。

但也可能出问题，比如这样：

```
Run, c:\mine\app\AutoHotkey\AutoHotkey.exe aa bb.ahk
```

AutoHotkey.exe 提示 aa 文件不存在，而实际我想运行的文件为 aa bb.ahk。同样的语法，效果却不同，区别在于要运行的软件是如何解析参数的。

如果在参数两边加上引号，就没有这样的问题了：

```
Run, notepad "aa bb.txt"

Run, c:\mine\app\AutoHotkey\AutoHotkey.exe "aa bb.ahk"

; 后边可以再加参数，但包含空格的参数都要有引号。
Run, c:\mine\app\AutoHotkey\AutoHotkey.exe "aa bb.ahk" "arg 1" "arg 2"
```

## 等待软件退出再继续运行

用 Run 命令启动软件的话，AHK 并不会等软件退出后才继续运行，在运行图形界面软件时，这也是我们通常的做法。如果我们需要让 AHK 暂停，直到运行起来的软件退出才继续，可以用 RunWait 命令取代 Run，除了这个区别，用法上和 Run 是一致的。

## 其他内容

除了将软件运行起来，很多时候我们也有一些其他需求。比如获取软件的输出（通常是命令行软件的输出，如 ipconfig），获取软件的退出码，获取软件进程的 PID 等等，我会在后续文章中详细讲到。
