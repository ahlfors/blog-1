Title: [基础] 如何在 AHK 下载网络内容到文件或变量
Date: 2017-09-18 20:29
Category: AHK

现在早已是网络时代了，完全单机而不需要联网的软件很少了。某些时候，用 AHK 写个脚本也是需要去网络获取数据的。

## 下载网络内容到本地文件

有些时候我们需要把一个网页、图片、音频等文件下载到本地，再做后续处理。在其他编程语言可能就比较麻烦了，比如先下载网络内容到一个字符串变量，然后创建一个文件，将字符串写入，再保存文件。而 AHK 内置的 UrlDownloadToFile 命令就是为这个场景量身定做的。

```
UrlDownloadToFile, URL, Filename
```

可以看到，这个命令的用法非常简单，第一个参数是文件的网络地址，第二个参数是文件的本地地址。如果第二个参数不是完整路径，文件会被下载到当前脚本的工作目录，一般情况就是脚本自身的目录。

比如我想下载 AHK 官网的 Logo 图片到 C:\test.png，就可以这样：

```
UrlDownloadToFile, http://ahkscript.org/logos/ahk_logo.png, C:\test.png
```

是不是非常简单。除了 HTTP，UrlDownloadToFile 还支持 HTTPS 和 FTP 协议。

## 下载网络内容到字符串变量

但有些情况，我们并不需要将网络内容保存到文件里，而是直接使用，比如直接显示在控件里。虽然也可以先写到文件再读出来，但这样做不仅多此一举，有额外的开销，还需要考虑共用临时文件、临时文件没被删除等问题。

AHK 并没有命令可以直接将网络内容下载到字符串变量，但可以使用 COM 对象 WinHttp.WinHttpRequest.5.1，官方文档中有一个例子，用起来倒也不是很麻烦。

```
whr := ComObjCreate("WinHttp.WinHttpRequest.5.1")
; Open() 的第三个参数代表同步或者异步，现在不用过多关注，true 就可以了
; 以后我们会仔细了解同步和异步
whr.Open("GET", "https://autohotkey.com/download/1.1/version.txt", true)
whr.Send()
whr.WaitForResponse()
version := whr.ResponseText
MsgBox, % version
```

这样就可以用 MsgBox 将从网络获取到的一个字符串弹出来了。但比起 UrlDownloadToFile 还是麻烦了不少，如果需要经常使用，还是很不方便的，这就需要封装成一个函数来使用，比如这样：

```
UrlDownloadToVar(url)
{
    static whr := ComObjCreate("WinHttp.WinHttpRequest.5.1")
    whr.Open("GET", url, true)
    whr.Send()
    whr.WaitForResponse()
    return whr.ResponseText
}
```

和刚才的代码差不多，但我这里 whr 是 static 类型的，因为只需要初始化一次就可以了，调用 ComObjCreate() 是有开销的。

但有些时候我们并不只需要传入一个 URL，可以还包含一些 HTTP Headers，而且使用 POST 方式访问，这就需要再改造下这个函数，比如这样：

```
UrlDownloadToVar(url, headers = "")
{
    static whr := ComObjCreate("WinHttp.WinHttpRequest.5.1")
    whr.Open("POST", url, true)

    if (headers != "")
    {
        for key, value in headers
        {
            whr.SetRequestHeader(key, value)
        }
    }

    whr.Send()
    whr.WaitForResponse()
    return whr.ResponseText
}
```

用起来也很方便，比如我要使用 POST 方式访问 test.org，携带两个 HTTP Headers，key1:value1 和 key2:value2：

```
result := UrlDownloadToVar("http://test.org"
    , { "key1" : "value1", "key2" : "value2" })
```

如果需要设置更多参数，比如超时时间，就需要查下 WinHttp.WinHttpRequest.5.1 的文档了，基本上都可以找到自己想要的功能。
